PREFIX fx:  <http://sparql.xyz/facade-x/ns/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?service ?calendarRule ?trip ?startDate ?endDate WHERE {

    SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/TRIPS.%format,%param,slice=%slice,strategy=%strategy,use-rdfs-member=true> {
      ?tripContainer rdfs:member [ a xyz:trip_id ; rdfs:member ?trip_id ] .
      ?tripContainer rdfs:member [ a xyz:service_id ; rdfs:member ?service_id  ] .
      BIND ( fx:entity( "http://transport.linkeddata.es/madrid/metro/trips/", ?trip_id ) AS ?trip ) .
    }

    SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/CALENDAR.%format,%param,slice=%slice,strategy=%strategy,use-rdfs-member=true> {
      ?calendareDatesContainer rdfs:member [ a xyz:service_id ; rdfs:member ?service_id  ] .
      ?calendareDatesContainer rdfs:member [ a xyz:start_date ; rdfs:member ?startDate  ] .
      ?calendareDatesContainer rdfs:member [ a xyz:end_date ; rdfs:member ?endDate  ] .
      BIND( fx:entity("http://transport.linkeddata.es/madrid/metro/calendar_rules/", ?service_id ) AS ?calendarRule ) .
      BIND( fx:entity("http://transport.linkeddata.es/madrid/metro/services/", ?service_id ) AS ?service ) .

      FILTER (xsd:date(?startDate) < "2017-07-02"^^xsd:date) .
      FILTER (xsd:date(?endDate) > "2017-07-02"^^xsd:date) .
    }

    # service2, calendar_date_rules
    SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/CALENDAR_DATES.%format,%param,slice=%slice,strategy=%strategy,use-rdfs-member=true> {
      ?calendarDatesContainer rdfs:member [ a xyz:service_id ; rdfs:member ?service_id  ] .
      FILTER NOT EXISTS {
          ?calendarDatesContainer rdfs:member [ a xyz:date ; rdfs:member "2017-07-02"  ] .
          ?calendarDatesContainer rdfs:member [ a xyz:exception_type ; rdfs:member "false"  ] .
      }
    }


}
