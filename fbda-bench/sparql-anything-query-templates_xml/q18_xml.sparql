PREFIX fx:  <http://sparql.xyz/facade-x/ns/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT  ?service ?serviceRule ?trip ?route ?longName ?shortName WHERE {

  SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/CALENDAR.%format,%param,slice=%slice,strategy=%strategy,use-rdfs-member=true> {
    ?calendarContainer  rdfs:member [ a xyz:service_id ; rdfs:member ?service_id ] .
    ?calendarContainer rdfs:member [ a  xyz:sunday ; rdfs:member "1"  ] .
    BIND( fx:entity( "http://transport.linkeddata.es/madrid/metro/services/", ?service_id ) AS ?service ) .
    BIND( fx:entity("http://transport.linkeddata.es/madrid/metro/calendar_rules/", ?service_id ) AS ?serviceRule ) .
  }

  SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/TRIPS.%format,%param,slice=%slice,strategy=%strategy,use-rdfs-member=true> {
    ?tripContainer rdfs:member [ a xyz:trip_id ; rdfs:member ?trip_id ] .
    ?tripContainer rdfs:member [ a xyz:service_id ; rdfs:member ?service_id ] .
    ?tripContainer  rdfs:member [ a xyz:route_id ; rdfs:member ?route_id ] .
    BIND ( fx:entity( "http://transport.linkeddata.es/madrid/metro/routes/", ?route_id ) AS ?route ) .
    BIND ( fx:entity( "http://transport.linkeddata.es/madrid/metro/trips/", ?trip_id ) AS ?trip ) .
  }

  SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/ROUTES.%format,%param,slice=%slice,strategy=%strategy,use-rdfs-member=true> {
    ?routeContainer rdfs:member [ a xyz:route_id ; rdfs:member ?route_id  ] .
    { ?routeContainer  rdfs:member [ a xyz:route_short_name ; rdfs:member ?shortName ] } UNION { ?routeContainer  rdfs:member [ a xyz:route_long_name ; rdfs:member ?longName ]  }
  }

}
